data Int where
    zero(): Int
    succ(Int): Int

function add(Int, Int): Int where
    add(x, zero()) = x
    add(x, succ(y)) = add(succ(x), y)

codata IntToInt where
    IntToInt.apply(Int): Int

function add1(): IntToInt where
    add1().apply(x) = succ(x)

function addder(Int): IntToInt where
    addder(x).apply(y) = add(x, y)

codata TwoIntToInt where
    TwoIntToInt.apply(Int, Int): Int

data ListOfInt where
    empty(): ListOfInt
    cons(Int, ListOfInt): ListOfInt

function map(IntToInt, ListOfInt): ListOfInt where
    map(f, empty()) = empty()
    map(f, cons(x, xs)) = cons(f.apply(x), map(f, xs))

codata StreamOfInt where
    StreamOfInt.head(): Int
    StreamOfInt.tail(): StreamOfInt

function mapStream(IntToInt, StreamOfInt): StreamOfInt where
    mapStream(f, s).head() = f.apply(s.head())
    mapStream(f, s).tail() = mapStream(f, s.tail())

function zipWith(TwoIntToInt, StreamOfInt, StreamOfInt): StreamOfInt where
    zipWith(f, a, b).head() = f.apply(a.head(), b.head())
    zipWith(f, a, b).tail() = zipWith(f, a.tail(), b.tail())

function zeroes(): StreamOfInt where
    zeroes().head() = zero()
    zeroes().tail() = zeroes()

function takeN(Int, StreamOfInt): ListOfInt where
    takeN(zero(), f)  = empty()
    takeN(succ(n), f) = cons(f.head(), takeN(n, f.tail()))
